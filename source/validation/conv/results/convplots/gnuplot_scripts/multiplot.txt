### Start multiplot (2x2 layout)
set terminal epslatex standalone monochrome size 8,7
set out 'convergence_tests.tex'

set multiplot layout 3,2 rowsfirst

set key off 

f(x) = 16

F1(x) = 16 + A1*exp(-(x+a1)/s1)
A1 = 4.1599
a1 = -2.46104
s1 = 2.39838
fit F1(x) "conv_Orbit-050.txt" via A1, a1 , s1

#set label 1 'e-0.5 Orbit' at graph 0.92,0.9 font ',8'
set title "(a) Orbit Test ($\\varepsilon =0.50$)" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset -1.50,0,0
set lmargin 9
set rmargin 1
plot [-0.5:7.5] [15:29] "conv_Orbit-050.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F1(x) with lines ls 1

F2(x) = 16 + A2*exp(-(x+a2)/s2)
fit F2(x) "conv_Orbit-095.txt" via A2, a2 , s2

#set label 1 'e-0.95 Orbit' at graph 0.92,0.9 font ',8'
set title "(b) Orbit Test ($\\varepsilon =0.95$)" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset 0.75,0,0
plot [-0.5:7.5] [15.7:19.3] "conv_Orbit-095.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F2(x) with lines ls 1

F3(x) = 16 + A3*exp(-(x+a3)/s3)
fit F3(x) "conv_SCMS1.txt" via A3, a3 , s3

#set label 1 'massive-massive' at graph 0.92,0.9 font ',8'
set title "(c) Two Particle Test: Massive-Massive Case" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset 2.25,0,0
plot [-0.5:7.5] [15.9987:16.0143] "conv_SCMS1.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F3(x) with lines ls 1

F4(x) = 16 + A4*exp(-(x+a4)/s4)
fit F4(x) "conv_SCML1.txt" via A4, a4 , s4

#set label 1 'massless-massless' at graph 0.92,0.9 font ',8'
set title "(d) Two Particle Test: Massless-Massless Case" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset 1.50,0,0
plot [-0.5:7.5] [15.984:16.176] "conv_SCML1.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F4(x) with lines ls 1

F5(x) = 16 + A5*exp(-(x+a5)/s5)
fit F5(x) "conv_SCMX1.txt" via A5, a5 , s5

#set label 1 'massive-massless' at graph 0.92,0.9 font ',8'
set title "(e) Two Particle Test: Massive-Massless Case" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset 1.25,0,0 
set xlabel "Smallest Timestep (in units of $h$)"
plot [-0.5:7.5] [15.991:16.099] "conv_SCMX1.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F5(x) with lines ls 1

F6(x) = 16 + A6*exp(-(x+a6)/s6)
fit F6(x) "conv_Five.txt" via A6, a6 , s6

#set label 1 'five particles' at graph 0.92,0.9 font ',8'
set title "(f) Five Particle Test" offset 0,-0.75,0
set ylabel "Convergence Factor Q" offset 1.50,0,0
set xlabel "Smallest Timestep (in units of $h$)" 
plot [-0.5:7.5] [15.976:16.264] "conv_Five.txt" with points pointtype 7 pointsize 0.85, \
	 f(x) with lines lt 3, \
	 F6(x) with lines ls 1

unset multiplot
unset output
unset terminal
### End multiplot

